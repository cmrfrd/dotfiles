;;;;
;;;;    Alexander Comerford .emacs configuration
;;;;
;;;;

;;; 
;;;  PACKAGE-REPOSITORY CONFIGURATION
;;;
;;;  In this section we add a few package repositories
;;;  to the package-archives list to be imported later.
;;;  After adding we will refresh our package contents
;;;

(package-initialize)
(require 'package)
(custom-set-variables
 ;; custom-set-variables was added by Custom.
 ;; If you edit it by hand, you could mess it up, so be careful.
 ;; Your init file should contain only one such instance.
 ;; If there is more than one, they won't work right.
 '(custom-safe-themes
   (quote
    ("da30a40c8003409e31cd35f4ea359bd34d0f48f77737bb113c338431cc2273bc" "b693a301c85a5e811698b5604e8c620b7e7914a39e74777d93dba9d3fe13e96b" "27da8fe05c56d8d189ddc04516e2e6ec5adccc08c08a90868428894f216cac6a" "548da76664b25eb9e7798812178b7c6db78573c5b47ab94b07d386841f9e37d2" "f71fbdf06ca876f6290ca9016cff20daa2d4b7e7fd743bf5b2b8fbb490958f85" "e1ac710b757f2c08aa29871e18d21ed764feda3dda98ed8a6751429d7c2dccc1" "d6590c8f4a63740fad0e1072c4549c6f3b41e198ce6420a8bb0521efe42f5b28" "8ba0a9fc75f2e3b4c254183e814b8b7b8bcb1ad6ca049fde50e338e1c61a12a0" "26d613485834c8498d96a664d970e19b7d5286c39a78452f492ae5572cf1bd21" "36c2b7efdc064944eb067e56c7ec65808a6cee0f63ce068b693fb30b110e57e5" "9f08dacc5b23d5eaec9cccb6b3d342bd4fdb05faf144bdcd9c4b5859ac173538" "cc8d032279b50d4c8a0caa9df6245cbbbfbfcc74f9b2ec26054ea4306fdf6b24" "e2ba9d9a5609c6809615d68b2e3ee6817079cd0195143385c24ee4e4a8e05c23" "e1ad20f721b90cc8e1f57fb8150f81e95deb7ecdec2062939389a4b66584c0cf" "2757944f20f5f3a2961f33220f7328acc94c88ef6964ad4a565edc5034972a53" "9399db70f2d5af9c6e82d4f5879b2354b28bc7b5e00cc8c9d568e5db598255c4" "f97e1d3abc6303757e38130f4003e9e0d76026fc466d9286d661499158a06d99" "e893b3d424a9b8b19fb8ab8612158c5b12b9071ea09bade71ba60f43c69355e6" "35eddbaa052a71ab98bbe0dbc1a5cb07ffbb5d569227ce00412579c2048e7699" "3adb42835b51c3a55bc6c1e182a0dd8d278c158769830da43705646196fc367e" "f4260b30a578a781b4c0858a4a0a6071778aaf69aed4ce2872346cbb28693c1a" default)))
 '(package-archives
   (quote
    (("melpa-stable" . "http://stable.melpa.org/packages/")
     ("melpa" . "http://melpa.org/packages/"))))
 '(package-selected-packages
   (quote
    (auto-complete-config go-autocomplete evil ensime unicode-fonts emojify org-journal show-paren-mode pylint org-id multiple-cursors w3m pymacs flymd bash-completion docker-tramp-compat helm-tramp helm-ag yaml-mode helm-projectile diff-hl magit multi-term company-shell company-ycmd docker-compose-mode jedi helm projectile shell-pop py-autopep8 docker neotree flycheck elpy company-anaconda pythonic markdown-mode all-the-icons kaolin-themes dockerfile-mode anaconda-mode docker-tramp company-jedi company use-package "company" "htmlize" ranger)))
 '(shell-pop-full-span t)
 '(shell-pop-shell-type
   (quote
    ("ansi-term" "*shell-pop*"
     (lambda nil
       (ansi-term shell-pop-term-shell)))))
 '(shell-pop-universal-key "C-x C-q")
 '(show-paren-mode t))
(package-refresh-contents)

;;;
;;; install the use-package package
;;; This enables us to install packages declaritively
;;; 
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))
(require 'use-package)

;;;
;;; Helm configuraiton
;;;
;;; Helm adds an additional level of usefulness allowing
;;; an easier way to autocomplete commands
;;;
(use-package helm
  :ensure t
  :bind (("M-x" . helm-M-x)
         ("C-x b" . helm-buffers-list)
         ("C-x f" . helm-find-files)
         ("C-x r b" . helm-bookmarks))
  :config
  (require 'helm-config)
  (helm-mode 1)

  ;; Globally enable fuzzy matching for helm-mode.
  (setq helm-mode-fuzzy-match t)
  (setq helm-completion-in-region-fuzzy-match t)

  (setq helm-M-x-fuzzy-match t)
  (setq helm-buffers-fuzzy-matching t)
  (setq helm-recentf-fuzzy-match t)

  (global-set-key [remap execute-extended-command] #'helm-smex)
  (global-set-key (kbd "M-X") #'helm-smex-major-mode-commands)

  ;; Disable Helm in the following functions.
  ;; See: https://github.com/emacs-helm/helm/wiki#customize-helm-mode
  (setq helm-completing-read-handlers-alist
        '((find-file-read-only . ido)
          (magit-gitignore . nil)
          (rename-file . ido)))

  ;; Enter directories with RET, same as ido
  ;; http://emacs.stackexchange.com/questions/3798/how-do-i-make-pressing-ret-in-helm-find-files-open-the-directory/7896#7896
  (defun helm-find-files-navigate-forward (orig-fun &rest args)
    (if (file-directory-p (helm-get-selection))
        (apply orig-fun args)
      (helm-maybe-exit-minibuffer)))
  (advice-add 'helm-execute-persistent-action :around #'helm-find-files-navigate-forward)

  (with-eval-after-load 'helm-files
    (define-key helm-find-files-map (kbd "<return>") 'helm-execute-persistent-action))

  ;; Don't show "." and ".." directories when finding files.
  ;; https://github.com/hatschipuh/better-helm
  (with-eval-after-load 'helm-files
    (advice-add 'helm-ff-filter-candidate-one-by-one
                :before-while 'no-dots-display-file-p))

  (defvar no-dots-whitelist nil
    "List of helm buffers in which to show dots.")

  (defun no-dots-in-white-listed-helm-buffer-p ()
    (member helm-buffer no-dots-whitelist))
 
  (defun no-dots-display-file-p (file)
    ;; in a whitelisted buffer display the file regardless of its name
    (or (no-dots-in-white-listed-helm-buffer-p)
        ;; not in a whitelisted buffer display all files
        ;; which does not end with /. /..
        (not (string-match "\\(?:/\\|\\`\\)\\.\\{1,2\\}\\'" file)))))

 ;; Install accompanying helm packages
(use-package helm-projectile
  :ensure t
  :init
  (setq projectile-completion-system 'helm)
  (helm-projectile-on))
(use-package helm-tramp
  :ensure t
  :bind ("C-c s" . helm-tramp)
  :config
   (setq tramp-default-method "docker"))
;;;
;;; HELM CONFIG END
;;;



;;
;; IDE like project
;;
(use-package projectile
  :ensure t
  :config
  (define-key projectile-mode-map (kbd "s-p") 'projectile-command-map)
  (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)
  (projectile-mode +1))

;;
;; Install file browsing with ranger
;;
(use-package ranger
  :ensure ranger)
(ranger-override-dired-mode t)

;;
;; IDE-like autocomplete.
;;
(use-package jedi
  :ensure t
  :init
  (add-hook 'python-mode-hook 'jedi:setup)
  (add-hook 'python-mode-hook 'jedi:ac-setup))
(use-package company
  :ensure t
  :config
  (global-company-mode)  
  (setq company-idle-delay 0)
  (setq company-minimum-prefix-length 3))
(use-package company-jedi
  :ensure t
  :defer t
  :init
  :config
  (setq jedi:setup-keys t)
  (setq jedi:complete-on-dot t)
  (add-hook 'python-mode-hook 'jedi:setup)
  (add-to-list 'company-backends 'company-jedi))
(use-package bash-completion
  :ensure t)


(use-package company :defer 30
  :ensure t
  :init (global-company-mode t)
  :config
  (defvar company-mode/enable-yas t
    "Enable yasnippet for all backends.")

  (defun company-mode/backend-with-yas (backend)
    (if (or (not company-mode/enable-yas)
            (and (listp backend)
                 (member 'company-yasnippet backend)))
        backend
      (append (if (consp backend) backend (list backend))
              '(:with company-yasnippet))))

  (setq company-backends
        (mapcar #'company-mode/backend-with-yas
                '((company-capf company-shell company-dabbrev company-abbrev
                                company-files company-etags company-keywords)))
        company-idle-delay 1.0
        company-tooltip-flip-when-above t)
  (use-package company-flx :defer t
    :ensure t
    :config (with-eval-after-load 'company
              (company-flx-mode +1)))
  (use-package company-ycmd :defer t
    :ensure t
    :config (company-ycmd-setup))
  (use-package company-shell :defer t
	       :ensure t))

;;
;; Syntax highlithing
;;
(use-package yaml-mode
  :ensure t)
(add-to-list 'auto-mode-alist '("\\.yml\\'" . yaml-mode))
(add-hook 'yaml-mode-hook
	  '(lambda ()
             (define-key yaml-mode-map "\C-m" 'newline-and-indent)))
(use-package pythonic
  :ensure pythonic)
(use-package anaconda-mode
  :ensure anaconda-mode)
(use-package company-anaconda
  :ensure company-anaconda)
(use-package py-autopep8
  :ensure t)
(use-package flycheck
  :ensure t)
(use-package flymake
  :ensure t)
(use-package xref
  :ensure t)

(use-package elpy
  :ensure t
  :config
  (progn
    (setq elpy-rpc-backend "jedi")
    (elpy-enable)))


(defun iambubbi/python-mode-hook ()
  (add-to-list 'company-backends 'company-jedi))
(defun iambubbi/company-anaconda-hook ()
  (add-to-list 'company-backends 'company-anaconda))
(defun iambubbi/flycheck-hook ()
  (global-flycheck-mode)
  (setq flycheck-check-syntax-automatically '(mode-enabled save)))

(add-hook 'python-mode-hook 'anaconda-mode)
(add-hook 'python-mode-hook 'anaconda-eldoc-mode)
(add-hook 'python-mode-hook 'iambubbi/python-mode-hook)
(add-hook 'python-mode-hook 'iambubbi/company-anaconda-hook)
(add-hook 'python-mode-hook 'iambubbi/flycheck-hook)
(add-hook 'python-mode-hook 'py-autopep8-enable-on-save)
(add-hook 'python-mode-hook 'jedi:setup)
(setq jedi:complete-on-dot t)


;; (defun show-fly-err-at-point ()
;;   "If the cursor is sitting on a flymake error, display the message in the minibuffer"
;;   (require 'cl)
;;   (interactive)
;;   (let ((line-no (line-number-at-pos)))
;;     (dolist (elem flymake-err-info)
;;       (if (eq (car elem) line-no)
;;       (let ((err (car (second elem))))
;;         (message "%s" (flymake-ler-text err)))))))

(add-hook 'python-mode-hook '(lambda () (define-key python-mode-map "\C-cn" 'flymake-goto-next-error)))
(add-hook 'python-mode-hook '(lambda () (define-key python-mode-map "\C-cp" 'flymake-goto-prev-error)))
;;(add-hook 'post-command-hook 'show-fly-err-at-point)

;;
;; Remote machine editor
;;
(use-package docker-tramp
  :ensure docker-tramp
  :config
  (setq docker-tramp-use-names t))
(use-package vagrant-tramp
  :ensure t)
(require 'docker-tramp-compat)
(use-package tramp
  :ensure t
  :defer t
  :config
  (setf tramp-persistency-file-name
        (concat temporary-file-directory "tramp-" (user-login-name))))

;;; python mode
;; The package is "python" but the mode is "python-mode":
(use-package python
  :mode ("\\.py\\'" . python-mode)
  :interpreter ("python" . python-mode))
(use-package ensime
  :ensure t
  :init
  (add-hook 'scala-mode-hook 'ensime-scala-mode-hook))
(use-package bash-completion
  :ensure t)
(use-package go-mode
  :ensure t
  :init
  (add-to-list 'auto-mode-alist '("\\.go\\'" . go-mode)))
(use-package go-autocomplete
  :ensure t)
(use-package auto-complete
  :ensure t
  :init (ac-config-default))

;;;; Dockerfiles
(use-package dockerfile-mode
  :ensure dockerfile-mode)
(require 'dockerfile-mode)
(add-to-list 'auto-mode-alist '("Dockerfile\\'" . dockerfile-mode))
(use-package docker-compose-mode
  :ensure t)


;;;; docker elisp
(use-package docker
  :ensure t
  :bind ("C-c d" . docker))
(use-package transpose-frame
  :ensure t)

;;;;; Markdown
(use-package markdown-mode
  :ensure t
  :commands (markdown-mode gfm-mode)
  :mode (("README\\.md\\'" . gfm-mode)
         ("\\.md\\'" . markdown-mode)
         ("\\.markdown\\'" . markdown-mode))
  :init (setq markdown-command "/usr/bin/pandoc"))

;;
;; NEOTREE
;;
(use-package neotree
  :ensure t
  :init
  (progn
    ;; Every time when the neotree window is opened, it will try to find current
    ;; file and jump to node.
    (setq-default neo-smart-open t)
    (setq-default neo-dont-be-alone t)
    (setq-default neo-window-fixed-size nil)
    (setq-default neo-show-hidden-files t))
  :config
  (progn
    (setq projectile-switch-project-action 'neotree-projectile-action)
    (setq neo-theme 'nerd) ; 'classic, 'nerd, 'ascii, 'arrow
    (setq neo-vc-integration '(face char))

    ;; Patch to fix vc integration
    (defun neo-vc-for-node (node)
      (let* ((backend (vc-backend node))
             (vc-state (when backend (vc-state node backend))))
        ;; (message "%s %s %s" node backend vc-state)
        (cons (cdr (assoc vc-state neo-vc-state-char-alist))
              (cl-case vc-state
                (up-to-date       neo-vc-up-to-date-face)
                (edited           neo-vc-edited-face)
                (needs-update     neo-vc-needs-update-face)
                (needs-merge      neo-vc-needs-merge-face)
                (unlocked-changes neo-vc-unlocked-changes-face)
                (added            neo-vc-added-face)
                (removed          neo-vc-removed-face)
                (conflict         neo-vc-conflict-face)
                (missing          neo-vc-missing-face)
                (ignored          neo-vc-ignored-face)
                (unregistered     neo-vc-unregistered-face)
                (user             neo-vc-user-face)
                (t                neo-vc-default-face)))))

    (defun modi/neotree-go-up-dir ()
      (interactive)
      (goto-char (point-min))
      (forward-line 2)
      (neotree-change-root))

    ;; http://emacs.stackexchange.com/a/12156/115
    (defun modi/find-file-next-in-dir (&optional prev)
      "Open the next file in the directory.
When PREV is non-nil, open the previous file in the directory."
      (interactive "P")
      (let ((neo-init-state (neo-global--window-exists-p)))
        (if (null neo-init-state)
            (neotree-show))
        (neo-global--select-window)
        (if (if prev
                (neotree-previous-line)
              (neotree-next-line))
            (progn
              (neo-buffer--execute nil
                                   (quote neo-open-file)
                                   (lambda (full-path &optional arg)
                                     (message "Reached dir: %s/" full-path)
                                     (if prev
                                         (neotree-next-line)
                                       (neotree-previous-line)))))
          (progn
            (if prev
                (message "You are already on the first file in the directory.")
              (message "You are already on the last file in the directory."))))
        (if (null neo-init-state)
            (neotree-hide))))

    (defun modi/find-file-prev-in-dir ()
      "Open the next file in the directory."
      (interactive)
      (modi/find-file-next-in-dir :prev))

    (bind-keys
     :map neotree-mode-map
      ("^" . modi/neotree-go-up-dir)
      ("<C-return>" . neotree-change-root)
      ("C" . neotree-change-root)
      ("c" . neotree-create-node)
      ("+" . neotree-create-node)
      ("d" . neotree-delete-node)
      ("r" . neotree-rename-node)))

  (add-to-list 'window-size-change-functions
               (lambda (frame)
                 (let ((neo-window (neo-global--get-window)))
                   (unless (null neo-window)
                     (setq neo-window-width (window-width neo-window))))))
  (global-set-key [f8] 'neotree-toggle))

(defun er-switch-to-previous-buffer ()
  "Switch to previously open buffer.
Repeated invocations toggle between the two most recently open buffers."
  (interactive)
  (switch-to-buffer (other-buffer (current-buffer) 1)))
(global-set-key (kbd "C-c b") #'er-switch-to-previous-buffer)

;;;
;;; Git configuration in emacs
;;;
(use-package magit
  :ensure t
  :bind (("C-x g" . magit-status)))
(use-package diff-hl
  :ensure t
  :config
  (global-diff-hl-mode +1)
  (add-hook 'dired-mode-hook 'diff-hl-dired-mode)
  (add-hook 'magit-post-refresh-hook 'diff-hl-magit-post-refresh))
(use-package ein
  :ensure t)


;; Interactive shell popping
;; include screen hook
(use-package shell-pop
  :ensure t
  :config

  ;; Run screen on shell pop startup
  (defun init-screen-in-process ()
    (transient-mark-mode)
    (local-unset-key (kbd "C-SPC"))
    (process-send-string "*shell-pop-1*" "screen\n"))
  (add-hook 'shell-pop-in-after-hook 'init-screen-in-process)

  ;; Remap C-left C-right
  ;; see https://emacs.stackexchange.com/questions/2396/how-do-i-configure-moving-by-and-deleting-words-in-ansi-term
  (defun term-send-Cright () (interactive) (term-send-raw-string "\e[1;5C"))
  (defun term-send-Cleft  () (interactive) (term-send-raw-string "\e[1;5D"))
  (define-key term-raw-map (kbd "C-<right>")      'term-send-Cright)
  (define-key term-raw-map (kbd "C-<left>")       'term-send-Cleft)

  (define-key term-raw-map (kbd "C-c <left>")  'windmove-left)
  (define-key term-raw-map (kbd "C-c <right>") 'windmove-right)
  (define-key term-raw-map (kbd "C-c <up>")    'windmove-up)
  (define-key term-raw-map (kbd "C-c <down>")  'windmove-down)
  
  ;; Add paste hook in terminal buffer
  (add-hook 'term-mode-hook
  	    (lambda ()
  			(define-key term-raw-map (kbd "C-y") 'term-paste)))

  )




(use-package buffer-move
  :ensure t)
;;
;; Send region to running emacs process
;;
(global-set-key (kbd "C-u") #'tws-region-to-process)
(defun tws-region-to-process (arg beg end)
  "Send the current region to a process buffer.
The first time it's called, will prompt for the buffer to
send to. Subsequent calls send to the same buffer, unless a
prefix argument is used (C-u), or the buffer no longer has an
active process."
  (interactive "P\nr")
  (when (or arg ;; user asks for selection
          (not (boundp 'tws-process-target)) ;; target not set
          ;; or target is not set to an active process:
          (not (process-live-p (get-buffer-process tws-process-target))))
    (let (procs buf)
     (setq procs (remove nil (seq-map
                  (lambda (el)
                    (when (setq buf (process-buffer el))
                      (buffer-name buf)))
                  (process-list))))
     (if (not procs) (error "No process buffers currently open.")
      (setq tws-process-target (completing-read "Process: " procs)))))  
  ;(process-send-region tws-process-target beg end))
  (process-send-string tws-process-target (buffer-substring beg end)))






;;
;; Multiepl cursors
;;
(use-package multiple-cursors
  :ensure t
  :config 
  (global-set-key (kbd "C->") 'mc/mark-next-like-this)
  (global-set-key (kbd "C-<") 'mc/mark-previous-like-this))



(use-package org
  :ensure t
  :defer t
  :bind (("C-c l" . org-store-link)
         ("C-c c" . org-capture)
         ("C-c a" . org-agenda)
         :map org-mode-map
         ;; ("C-h" . org-delete-backward-char)
         ("C-c !" . org-time-stamp-inactive))
  :mode ("\\.org$" . org-mode))
(use-package org-journal
  :ensure t)





;;;
;;; Emacs configuration
;;;
;;; Hot keys, emacs style, themes, and functions
;;;
(setq cursor-type 'bar)
(setq make-backup-files nil) ; stop creating backup~ files
(setq auto-save-default nil) ; stop creating #autosave# files

;; hotkey window resizingx
(global-set-key (kbd "C-M-<left>") 'shrink-window-horizontally)
(global-set-key (kbd "C-M-<right>") 'enlarge-window-horizontally)
(global-set-key (kbd "C-M-<down>") 'shrink-window)
(global-set-key (kbd "C-M-<up>") 'enlarge-window)

;; window switching hotkeys
(global-set-key (kbd "C-c <left>")  'windmove-left)
(global-set-key (kbd "C-c <right>") 'windmove-right)
(global-set-key (kbd "C-c <up>")    'windmove-up)
(global-set-key (kbd "C-c <down>")  'windmove-down)

;; add line numbers
(global-linum-mode t)

;; hotkey scrolling
(defun next-line-and-recenter () (interactive) (next-line) (recenter))
(defun previous-line-and-recenter () (interactive) (previous-line) (recenter))
(global-set-key (kbd "C-n") 'next-line-and-recenter)
(global-set-key (kbd "C-p") 'previous-line-and-recenter) 

;; Configure backup files
(setq backup-directory-alist '(("." . "~/.emacs.d/backups"))
      auto-save-file-name-transforms '((".*" "~/.emacs.d/auto-save-list/" t))
      delete-old-versions t
      version-control t
      vc-make-backup-files t
      backup-by-copying t
      kept-new-versions 2
      kept-old-versions 2)

;; no menu bar
(if (fboundp 'menu-bar-mode) (menu-bar-mode -1))

;; Theme installation
(use-package all-the-icons
  :ensure all-the-icons)
(use-package kaolin-themes
  :ensure t
  :config
  (kaolin-treemacs-theme)
  (set-face-attribute 'region nil :background "#35393C")
  (set-face-foreground 'linum "#42B8A7")
  (setq kaolin-themes-hl-line-colored t)       
  (setq kaolin-themes-distinct-fringe t)  
  (setq kaolin-themes-distinct-company-scrollbar t)  
  )
